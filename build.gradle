buildscript {
	repositories {
		mavenCentral()
		maven { url 'https://s01.oss.sonatype.org' }
		mavenLocal()
		google()
		gradlePluginPortal()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
		maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
	}
	dependencies {
		classpath "org.wisepersist:gwt-gradle-plugin:$gwtPluginVersion"
		classpath "com.badlogicgames.gdx:gdx-tools:$gdxVersion"

		// This follows advice from https://blog.gradle.org/log4j-vulnerability
		constraints {
			classpath("org.apache.logging.log4j:log4j-core") {
				version {
					strictly("[2.17, 3[")
					prefer("2.17.0")
				}
				because("CVE-2021-44228, CVE-2021-45046, CVE-2021-45105: Log4j vulnerable to remote code execution and other critical security vulnerabilities")
			}
		}
	}
}

import com.badlogic.gdx.tools.texturepacker.TexturePacker
task texturePacker {
	doLast {
		TexturePacker.process("${project.rootDir}/sourceAssets/Atlases/Bg", "${project.rootDir}/assets/Atlases", "Bg")
		TexturePacker.process("${project.rootDir}/sourceAssets/Atlases/Main", "${project.rootDir}/assets/Atlases", "Main")
	}
}

allprojects {
	apply plugin: 'eclipse'
	apply plugin: 'idea'
}

configure(subprojects) {
	apply plugin: 'java-library'
	sourceCompatibility = 1.8
	compileJava {
		options.incremental = true
	}
	dependencies {
		// This follows advice from https://blog.gradle.org/log4j-vulnerability
		constraints {
			implementation("org.apache.logging.log4j:log4j-core") {
				version {
					strictly("[2.17, 3[")
					prefer("2.17.0")
				}
				because("CVE-2021-44228, CVE-2021-45046, CVE-2021-45105: Log4j vulnerable to remote code execution and other critical security vulnerabilities")
			}
		}
	}

	compileJava.dependsOn texturePacker
}

subprojects {
	version = '0.0.1'
	ext.appName = 'SuspiciouslyAdverseRollercoaster'
	repositories {
		mavenCentral()
		maven { url 'https://s01.oss.sonatype.org' }
		mavenLocal()
		gradlePluginPortal()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
		maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
		maven { url 'https://jitpack.io' }
	}
}

eclipse.project.name = 'SuspiciouslyAdverseRollercoaster' + '-parent'
